@echo off
setlocal enabledelayedexpansion

set "SCRIPT_DIR=%~dp0"
pushd "%SCRIPT_DIR%"

set "MODE=%~1"
if "%MODE%"=="" set "MODE=build"

if /I "%MODE%"=="build" (
    rem ok
) else if /I "%MODE%"=="release" (
    rem ok
) else if /I "%MODE%"=="dry-run" (
    rem ok
) else (
    echo Usage: build.bat ^<build^|release^|dry-run^>
    exit /b 1
)

set "DEFAULT_SIGNTOOL=C:\Program Files (x86)\Windows Kits\10\bin\10.0.26100.0\x64\signtool.exe"
if defined SIGNTOOL_PATH (
    set "SIGNTOOL_EXE=%SIGNTOOL_PATH%"
) else (
    set "SIGNTOOL_EXE=%DEFAULT_SIGNTOOL%"
)
if not defined GITHUB_REPO_SLUG set "GITHUB_REPO_SLUG=serrebidev/BlindRSS"
if not defined RELEASE_REMOTE set "RELEASE_REMOTE=origin"

if /I "%MODE%"=="dry-run" (
    call :detect_python
    if errorlevel 1 exit /b 1
    set "TOOL_PY=!PYTHON_EXE!"
    call :verify_release_remote
    if errorlevel 1 exit /b 1
    call :compute_next_version
    if errorlevel 1 exit /b 1
    call :find_inno_setup
    if errorlevel 1 exit /b 1
    echo [Dry Run] Latest tag: !LATEST_TAG!
    echo [Dry Run] Next version: v!NEXT_VERSION! [!BUMP! bump]
    echo [Dry Run] Inno Setup compiler: !INNO_SETUP_EXE!
    echo [Dry Run] Would bump core/version.py, compile translations, build, sign with "%SIGNTOOL_EXE%", create the portable ZIP and Program Files installer, generate release notes, update CHANGELOG.md, generate the manifest, tag, push to "%RELEASE_REMOTE%", create a GitHub release in "%GITHUB_REPO_SLUG%", and dispatch the macOS/Linux GitHub Actions asset build.
    goto :done
)

call :setup_venv
if errorlevel 1 exit /b 1
set "TOOL_PY=%VENV_PYTHON%"

if /I "%MODE%"=="release" (
    call :verify_release_remote
    if errorlevel 1 exit /b 1
    call :compute_next_version
    if errorlevel 1 exit /b 1
    set "VERSION_NO_V=!NEXT_VERSION!"
    set "VERSION_TAG=!NEXT_TAG!"
    rem Ship the browser versions this machine actually runs, so the default
    rem User-Agent does not age into a bot signal (core/user_agents.py).
    echo [BlindRSS Build] Refreshing browser User-Agent versions...
    "%TOOL_PY%" tools\refresh_user_agents.py

    echo [BlindRSS Build] Bumping version to !VERSION_TAG!...
    "%TOOL_PY%" tools\release.py bump-version --version !VERSION_NO_V!
    if errorlevel 1 exit /b 1

    call :build_app
    if errorlevel 1 exit /b 1
    call :sign_exe
    if errorlevel 1 exit /b 1
    call :zip_release
    if errorlevel 1 exit /b 1
    call :hash_zip
    if errorlevel 1 exit /b 1
    call :build_installer
    if errorlevel 1 exit /b 1
    call :sign_installer
    if errorlevel 1 exit /b 1
    call :hash_installer
    if errorlevel 1 exit /b 1
    call :write_notes
    if errorlevel 1 exit /b 1
    call :update_changelog
    if errorlevel 1 exit /b 1
    call :write_manifest
    if errorlevel 1 exit /b 1
    call :git_release
    if errorlevel 1 exit /b 1
    call :dispatch_cross_platform_release
    if errorlevel 1 exit /b 1
) else (
    call :compute_current_version
    if errorlevel 1 exit /b 1
    set "VERSION_NO_V=!CURRENT_VERSION!"
    set "VERSION_TAG=v!CURRENT_VERSION!"

    call :build_app
    if errorlevel 1 exit /b 1
    call :sign_exe
    if errorlevel 1 exit /b 1
    call :zip_release
    if errorlevel 1 exit /b 1
    call :hash_zip
    if errorlevel 1 exit /b 1
    call :build_installer
    if errorlevel 1 exit /b 1
    call :sign_installer
    if errorlevel 1 exit /b 1
    call :hash_installer
    if errorlevel 1 exit /b 1
)

goto :done

:detect_python
set "PYTHON_EXE="
where /q py
if not errorlevel 1 (
    for /f "delims=" %%P in ('py -3.14 -c "import sys; print(sys.executable)" 2^>nul') do (
        set "PYTHON_EXE=%%P"
    )
)
if defined PYTHON_EXE exit /b 0

where /q python
if errorlevel 1 (
    echo [X] Python not found. Install Python 3.14+ and ensure it is available ^(python/py^).
    exit /b 1
)
for /f "delims=" %%P in ('python -c "import sys; print(sys.executable) if sys.version_info >= (3, 14) else sys.exit(1)" 2^>nul') do (
    set "PYTHON_EXE=%%P"
)
if not defined PYTHON_EXE (
    echo [X] Python 3.14+ is required. Install Python 3.14 and ensure it is available as "py -3.14" or "python".
    exit /b 1
)
exit /b 0

:setup_venv
set "VENV_DIR=%SCRIPT_DIR%.venv"
echo [BlindRSS Build] Preparing Python environment...
call :detect_python
if errorlevel 1 exit /b 1
for /f "delims=" %%V in ('"%PYTHON_EXE%" -c "import sys; print(f'{sys.version_info[0]}.{sys.version_info[1]}')" 2^>nul') do (
    set "TARGET_PYTHON_VERSION=%%V"
)

if exist "%VENV_DIR%" (
    if not exist "%VENV_DIR%\Scripts\python.exe" (
        echo [BlindRSS Build] Existing virtualenv is incomplete. Recreating...
        rd /s /q "%VENV_DIR%"
    ) else (
        set "EXISTING_VENV_VERSION="
        for /f "delims=" %%V in ('"%VENV_DIR%\Scripts\python.exe" -c "import sys; print(f'{sys.version_info[0]}.{sys.version_info[1]}')" 2^>nul') do (
            set "EXISTING_VENV_VERSION=%%V"
        )
        if defined TARGET_PYTHON_VERSION if defined EXISTING_VENV_VERSION if not "!EXISTING_VENV_VERSION!"=="!TARGET_PYTHON_VERSION!" (
            echo [BlindRSS Build] Existing virtualenv uses Python !EXISTING_VENV_VERSION!, recreating with Python !TARGET_PYTHON_VERSION!...
            rd /s /q "%VENV_DIR%"
        )
    )
)

if not exist "%VENV_DIR%" (
    "%PYTHON_EXE%" -m venv "%VENV_DIR%"
    if errorlevel 1 exit /b 1
)

set "VENV_PYTHON=%VENV_DIR%\Scripts\python.exe"
set "VENV_PIP=%VENV_DIR%\Scripts\pip.exe"
set "VENV_PYINSTALLER=%VENV_DIR%\Scripts\pyinstaller.exe"

if not exist "%VENV_PYTHON%" (
    echo [X] Failed to create virtual environment at "%VENV_DIR%".
    echo [X] Ensure Python is installed with venv support and try deleting the .venv folder.
    exit /b 1
)

echo [BlindRSS Build] Updating build tools...
"%VENV_PYTHON%" -m pip install --upgrade pip
if errorlevel 1 exit /b 1
"%VENV_PYTHON%" -m pip install --upgrade pyinstaller packaging
if errorlevel 1 exit /b 1

echo [BlindRSS Build] Installing dependencies from requirements.txt...
if exist "requirements.txt" (
	    "%VENV_PYTHON%" -m pip install -r requirements.txt
	    if errorlevel 1 (
	        echo [WARN] Dependency installation failed. Retrying without optional native dependency: webrtcvad
	        set "REQ_NO_WEBRTCVAD=%TEMP%\blindrss_requirements_no_webrtcvad.txt"
	        "%VENV_PYTHON%" tools\build_utils.py filter-requirements --input "requirements.txt" --output "!REQ_NO_WEBRTCVAD!" --exclude webrtcvad --exclude webrtcvad-wheels
	        "%VENV_PYTHON%" -m pip install -r "!REQ_NO_WEBRTCVAD!"
	        set "RC=!ERRORLEVEL!"
	        del /f /q "!REQ_NO_WEBRTCVAD!" >nul 2>nul
	        if not "!RC!"=="0" exit /b !RC!
	    )
) else (
    echo [WARN] requirements.txt not found. Installing defaults...
    "%VENV_PYTHON%" -m pip install wxPython feedparser requests beautifulsoup4 yt-dlp python-dateutil mutagen python-vlc pychromecast async-upnp-client pyatv trafilatura^>=2.1.0 webrtcvad-wheels brotli curl_cffi html5lib lxml setuptools^<81
    if errorlevel 1 exit /b 1
)

echo [BlindRSS Build] Upgrading the text-extraction stack to latest releases...
REM requirements.txt installs satisfy ">=" pins without ever upgrading, so the
REM venv would stay on whatever trafilatura it first got. Full-text quality
REM tracks these packages, so pull their latest releases on every build.
"%VENV_PYTHON%" -m pip install --upgrade trafilatura htmldate justext courlan
if errorlevel 1 (
    echo [WARN] Extraction-stack upgrade failed; continuing with the pinned versions.
)

echo [BlindRSS Build] Ensuring yt-dlp binary is present...
"%VENV_PYTHON%" -c "from core.dependency_check import _ensure_yt_dlp_cli; _ensure_yt_dlp_cli()"
if not exist "%SCRIPT_DIR%bin\\yt-dlp.exe" (
    echo [BlindRSS Build] Downloading yt-dlp.exe...
    "%VENV_PYTHON%" -c "import pathlib, urllib.request; p=pathlib.Path(r'%SCRIPT_DIR%bin\\yt-dlp.exe'); p.parent.mkdir(parents=True, exist_ok=True); urllib.request.urlretrieve('https://github.com/yt-dlp/yt-dlp/releases/latest/download/yt-dlp.exe', p.as_posix())"
)
if not exist "%SCRIPT_DIR%bin\\yt-dlp.exe" (
    echo [X] yt-dlp.exe not found in "%SCRIPT_DIR%bin". Build cannot continue.
    exit /b 1
)

echo [BlindRSS Build] Ensuring Deno binary is present...
if not exist "%SCRIPT_DIR%bin\\deno.exe" (
    echo [BlindRSS Build] Downloading Deno...
    "%VENV_PYTHON%" -c "import pathlib, urllib.request, zipfile, io; url='https://github.com/denoland/deno/releases/latest/download/deno-x86_64-pc-windows-msvc.zip'; bin_path=pathlib.Path(r'%SCRIPT_DIR%bin'); bin_path.mkdir(parents=True, exist_ok=True); print('Downloading Deno...'); data=urllib.request.urlopen(url).read(); print('Extracting...'); z=zipfile.ZipFile(io.BytesIO(data)); z.extract('deno.exe', bin_path); z.close()"
)
if not exist "%SCRIPT_DIR%bin\\deno.exe" (
    echo [X] deno.exe not found in "%SCRIPT_DIR%bin". Build cannot continue.
    exit /b 1
)

echo [BlindRSS Build] Ensuring NVDA Controller Client is present...
"%VENV_PYTHON%" tools\ensure_nvda_controller_client.py --dest "%SCRIPT_DIR%bin"
if errorlevel 1 exit /b 1
if not exist "%SCRIPT_DIR%bin\\nvdaControllerClient.dll" (
    echo [X] nvdaControllerClient.dll not found in "%SCRIPT_DIR%bin". Build cannot continue.
    exit /b 1
)
exit /b 0

:compute_next_version
echo [BlindRSS Build] Syncing tags...
git fetch "%RELEASE_REMOTE%" --tags --prune >nul 2>nul
if errorlevel 1 echo [WARN] Failed to fetch tags from "%RELEASE_REMOTE%". Using local tags.
for /f "usebackq tokens=1* delims==" %%A in (`"%TOOL_PY%" tools\release.py next-version`) do (
    set "%%A=%%B"
)
if not defined NEXT_VERSION (
    echo [X] Failed to compute next version.
    exit /b 1
)
exit /b 0

:compute_current_version
for /f "usebackq tokens=1* delims==" %%A in (`"%TOOL_PY%" tools\release.py current-version`) do (
    set "%%A=%%B"
)
if not defined CURRENT_VERSION (
    echo [X] Failed to read current version.
    exit /b 1
)
exit /b 0

:verify_release_remote
set "REMOTE_URL="
for /f "delims=" %%U in ('git remote get-url "%RELEASE_REMOTE%" 2^>nul') do (
    set "REMOTE_URL=%%U"
)
if not defined REMOTE_URL (
    echo [X] Git remote "%RELEASE_REMOTE%" was not found.
    exit /b 1
)
echo(!REMOTE_URL! | findstr /I /C:"%GITHUB_REPO_SLUG%" >nul
if errorlevel 1 (
    echo [X] Git remote "%RELEASE_REMOTE%" points to "!REMOTE_URL!".
    echo [X] Expected a GitHub remote for "%GITHUB_REPO_SLUG%".
    echo [X] Update it with: git remote set-url %RELEASE_REMOTE% https://github.com/%GITHUB_REPO_SLUG%.git
    exit /b 1
)
exit /b 0

:write_notes
set "DIST_DIR=%SCRIPT_DIR%dist"
if not exist "%DIST_DIR%" mkdir "%DIST_DIR%"
set "RELEASE_NOTES=%DIST_DIR%\release-notes-%VERSION_TAG%.md"
set "SUMMARY_FILE=%DIST_DIR%\release-notes-summary.txt"
echo [BlindRSS Build] Generating release notes...
"%TOOL_PY%" tools\release.py write-notes --from-tag "%LATEST_TAG%" --to-tag "%VERSION_TAG%" --output "%RELEASE_NOTES%" --summary-output "%SUMMARY_FILE%"
if errorlevel 1 exit /b 1
exit /b 0

:update_changelog
echo [BlindRSS Build] Updating CHANGELOG.md...
"%TOOL_PY%" tools\release.py update-changelog --version-tag "%VERSION_TAG%" --notes-file "%RELEASE_NOTES%" --output "%SCRIPT_DIR%CHANGELOG.md"
if errorlevel 1 exit /b 1
exit /b 0

:build_app
echo [BlindRSS Build] Ensuring config.json exists...
if not exist "%SCRIPT_DIR%config.json" (
    echo { "active_provider": "local" } > "%SCRIPT_DIR%config.json"
)

rem Preserve local test data (e.g. rss.db) between iterative builds.
rem This is only for MODE=build; release builds must always be clean.
set "PRESERVE_DIR="
if /I "%MODE%"=="build" (
    set "DIST_APP_DIR=%SCRIPT_DIR%dist\\BlindRSS"
	    if exist "!DIST_APP_DIR!\\rss.db" (
	        set "PRESERVE_DIR=%TEMP%\\BlindRSS_dist_preserve_!RANDOM!"
	        echo [BlindRSS Build] Preserving dist user data...
	        call :copy_user_data "!DIST_APP_DIR!" "!PRESERVE_DIR!"
	    )
	)

echo [BlindRSS Build] Cleaning previous build...
if exist "%SCRIPT_DIR%build" rd /s /q "%SCRIPT_DIR%build"
if exist "%SCRIPT_DIR%dist" rd /s /q "%SCRIPT_DIR%dist"

echo [BlindRSS Build] Compiling translation catalogs...
"%TOOL_PY%" tools\compile_translations.py
if errorlevel 1 (
    call :restore_preserved_dist_data
    exit /b 1
)

echo [BlindRSS Build] Running PyInstaller (main.spec)...
if exist "main.spec" (
    "%VENV_PYTHON%" -m PyInstaller --clean --noconfirm main.spec
) else (
    echo [WARN] main.spec not found. Running basic one-file build...
    "%VENV_PYTHON%" -m PyInstaller --onefile --noconfirm --name BlindRSS main.py
)
set "PYI_RC=%ERRORLEVEL%"
if not "%PYI_RC%"=="0" (
    call :restore_preserved_dist_data
    exit /b %PYI_RC%
)

echo [BlindRSS Build] Refreshing VLC plugins cache...
set "VLC_DIR=C:\Program Files\VideoLAN\VLC"
if not exist "%VLC_DIR%\vlc-cache-gen.exe" set "VLC_DIR=C:\Program Files (x86)\VideoLAN\VLC"
set "VLC_CACHE_GEN=%VLC_DIR%\vlc-cache-gen.exe"

set "DIST_PLUGINS=%SCRIPT_DIR%dist\BlindRSS\_internal\plugins"
if not exist "%DIST_PLUGINS%" set "DIST_PLUGINS=%SCRIPT_DIR%dist\BlindRSS\plugins"

if exist "%DIST_PLUGINS%" (
    if exist "%DIST_PLUGINS%\plugins.dat" del /f /q "%DIST_PLUGINS%\plugins.dat"
    if exist "%VLC_CACHE_GEN%" (
        "%VLC_CACHE_GEN%" "%DIST_PLUGINS%" >nul 2>nul
    ) else (
        echo [WARN] vlc-cache-gen.exe not found. Plugins cache will be rebuilt at runtime.
    )
) else (
    echo [WARN] VLC plugins directory not found in dist. Skipping cache refresh.
)

echo [BlindRSS Build] Staging companion files into dist...
if exist "%SCRIPT_DIR%README.md" copy /Y "%SCRIPT_DIR%README.md" "%SCRIPT_DIR%dist\README.md" >nul
if exist "%SCRIPT_DIR%update_helper.bat" copy /Y "%SCRIPT_DIR%update_helper.bat" "%SCRIPT_DIR%dist\BlindRSS\update_helper.bat" >nul

call :restore_preserved_dist_data

echo [BlindRSS Build] Copying exe to repo root...
if exist "%SCRIPT_DIR%dist\BlindRSS.exe" copy /Y "%SCRIPT_DIR%dist\BlindRSS.exe" "%SCRIPT_DIR%BlindRSS.exe" >nul
exit /b 0

:restore_preserved_dist_data
if not defined PRESERVE_DIR exit /b 0
if not exist "!PRESERVE_DIR!\\rss.db" goto :restore_preserved_dist_data_cleanup

	echo [BlindRSS Build] Restoring preserved dist user data...
	call :copy_user_data "!PRESERVE_DIR!" "%SCRIPT_DIR%dist\\BlindRSS"

:restore_preserved_dist_data_cleanup
	rd /s /q "!PRESERVE_DIR!" >nul 2>nul
	set "PRESERVE_DIR="
	exit /b 0

:copy_user_data
	set "SRC=%~1"
	set "DEST=%~2"
	if not exist "!DEST!" mkdir "!DEST!" >nul 2>nul
	for %%F in (rss.db rss.db-wal rss.db-shm) do (
	    if exist "!SRC!\\%%F" copy /Y "!SRC!\\%%F" "!DEST!\\%%F" >nul 2>nul
	)
	if exist "!SRC!\\podcasts" xcopy /E /I /Y "!SRC!\\podcasts" "!DEST!\\podcasts" >nul 2>nul
	exit /b 0

:sign_exe
if /I "%MODE%"=="build" (
    if defined SKIP_SIGN (
        echo [BlindRSS Build] SKIP_SIGN is set. Skipping Authenticode signing.
        exit /b 0
    )
    if not exist "%SIGNTOOL_EXE%" (
        echo [WARN] signtool.exe not found at "%SIGNTOOL_EXE%". Skipping signing for an unsigned build.
        exit /b 0
    )
)
if not exist "%SIGNTOOL_EXE%" (
    echo [X] signtool.exe not found at "%SIGNTOOL_EXE%".
    exit /b 1
)
set "EXE_PATH=%SCRIPT_DIR%dist\BlindRSS\BlindRSS.exe"
if not exist "%EXE_PATH%" set "EXE_PATH=%SCRIPT_DIR%dist\BlindRSS.exe"
if not exist "%EXE_PATH%" (
    echo [X] BlindRSS.exe not found in dist output.
    exit /b 1
)
echo [BlindRSS Build] Signing "%EXE_PATH%"...
"%SIGNTOOL_EXE%" sign /fd SHA256 /tr http://timestamp.digicert.com /td SHA256 /a "%EXE_PATH%"
if errorlevel 1 exit /b 1
set "SIGNING_THUMBPRINT="
if defined SIGN_CERT_THUMBPRINT (
    set "SIGNING_THUMBPRINT=%SIGN_CERT_THUMBPRINT%"
) else (
    set "SIGNING_THUMBPRINT_FILE=%TEMP%\\BlindRSS_thumbprint.txt"
    "%TOOL_PY%" tools\build_utils.py signtool-thumbprint --signtool "%SIGNTOOL_EXE%" --exe "%EXE_PATH%" --output "!SIGNING_THUMBPRINT_FILE!"
    if exist "!SIGNING_THUMBPRINT_FILE!" set /p SIGNING_THUMBPRINT=<"!SIGNING_THUMBPRINT_FILE!"
    if exist "!SIGNING_THUMBPRINT_FILE!" del /f /q "!SIGNING_THUMBPRINT_FILE!" >nul 2>&1
    if defined SIGNING_THUMBPRINT set "SIGNING_THUMBPRINT=!SIGNING_THUMBPRINT: =!"
)
exit /b 0

:zip_release
set "ZIP_NAME=BlindRSS-v%VERSION_NO_V%.zip"
set "ZIP_PATH=%SCRIPT_DIR%dist\%ZIP_NAME%"
if exist "%ZIP_PATH%" del /f /q "%ZIP_PATH%"
echo [BlindRSS Build] Creating zip "%ZIP_NAME%"...
powershell -NoProfile -Command "Compress-Archive -Path '%SCRIPT_DIR%dist\BlindRSS' -DestinationPath '%ZIP_PATH%' -Force" >nul
if errorlevel 1 exit /b 1
copy /Y "%ZIP_PATH%" "%SCRIPT_DIR%BlindRSS.zip" >nul
exit /b 0

:hash_zip
set "ZIP_SHA="
set "ZIP_HASH_FILE=%TEMP%\\BlindRSS_zip_hash.txt"
"%TOOL_PY%" tools\build_utils.py sha256 --input "%ZIP_PATH%" --output "!ZIP_HASH_FILE!"
if exist "!ZIP_HASH_FILE!" set /p ZIP_SHA=<"!ZIP_HASH_FILE!"
if exist "!ZIP_HASH_FILE!" del /f /q "!ZIP_HASH_FILE!" >nul 2>&1
if not defined ZIP_SHA (
    echo [X] Failed to compute SHA-256.
    exit /b 1
)
exit /b 0

:find_inno_setup
set "INNO_SETUP_EXE="
if defined INNO_SETUP_COMPILER if exist "%INNO_SETUP_COMPILER%" set "INNO_SETUP_EXE=%INNO_SETUP_COMPILER%"
if defined INNO_SETUP_EXE exit /b 0

for %%I in (
    "%LOCALAPPDATA%\Programs\Inno Setup 6\ISCC.exe"
    "%ProgramFiles%\Inno Setup 6\ISCC.exe"
    "%ProgramFiles(x86)%\Inno Setup 6\ISCC.exe"
    "%LOCALAPPDATA%\Programs\Inno Setup 7\ISCC.exe"
    "%ProgramFiles%\Inno Setup 7\ISCC.exe"
    "%ProgramFiles(x86)%\Inno Setup 7\ISCC.exe"
) do (
    if not defined INNO_SETUP_EXE if exist "%%~fI" set "INNO_SETUP_EXE=%%~fI"
)
if defined INNO_SETUP_EXE exit /b 0

for /f "delims=" %%I in ('where ISCC.exe 2^>nul') do (
    if not defined INNO_SETUP_EXE set "INNO_SETUP_EXE=%%I"
)
if defined INNO_SETUP_EXE exit /b 0

echo [X] Inno Setup compiler ISCC.exe was not found.
echo [X] Install Inno Setup 6 per-user or system-wide, add ISCC.exe to PATH,
echo [X] or set INNO_SETUP_COMPILER to its full path.
exit /b 1

:build_installer
call :find_inno_setup
if errorlevel 1 exit /b 1
set "INSTALLER_NAME=BlindRSS-Setup-v%VERSION_NO_V%.exe"
set "INSTALLER_PATH=%SCRIPT_DIR%dist\%INSTALLER_NAME%"
if exist "%INSTALLER_PATH%" del /f /q "%INSTALLER_PATH%"
echo [BlindRSS Build] Compiling Program Files installer with "%INNO_SETUP_EXE%"...
"%INNO_SETUP_EXE%" /DMyAppVersion=%VERSION_NO_V% "%SCRIPT_DIR%installer\BlindRSS.iss"
if errorlevel 1 exit /b 1
if not exist "%INSTALLER_PATH%" (
    echo [X] Installer output was not created at "%INSTALLER_PATH%".
    exit /b 1
)
exit /b 0

:sign_installer
if /I "%MODE%"=="build" (
    if defined SKIP_SIGN (
        echo [BlindRSS Build] SKIP_SIGN is set. Skipping installer Authenticode signing.
        exit /b 0
    )
    if not exist "%SIGNTOOL_EXE%" (
        echo [WARN] signtool.exe not found at "%SIGNTOOL_EXE%". Installer remains unsigned.
        exit /b 0
    )
)
if not exist "%SIGNTOOL_EXE%" (
    echo [X] signtool.exe not found at "%SIGNTOOL_EXE%".
    exit /b 1
)
if not exist "%INSTALLER_PATH%" (
    echo [X] Installer not found at "%INSTALLER_PATH%".
    exit /b 1
)
echo [BlindRSS Build] Signing "%INSTALLER_PATH%"...
"%SIGNTOOL_EXE%" sign /fd SHA256 /tr http://timestamp.digicert.com /td SHA256 /a "%INSTALLER_PATH%"
if errorlevel 1 exit /b 1
exit /b 0

:hash_installer
set "INSTALLER_SHA="
set "INSTALLER_HASH_FILE=%TEMP%\BlindRSS_installer_hash.txt"
"%TOOL_PY%" tools\build_utils.py sha256 --input "%INSTALLER_PATH%" --output "!INSTALLER_HASH_FILE!"
if exist "!INSTALLER_HASH_FILE!" set /p INSTALLER_SHA=<"!INSTALLER_HASH_FILE!"
if exist "!INSTALLER_HASH_FILE!" del /f /q "!INSTALLER_HASH_FILE!" >nul 2>&1
if not defined INSTALLER_SHA (
    echo [X] Failed to compute installer SHA-256.
    exit /b 1
)
exit /b 0

:write_manifest
set "MANIFEST_PATH=%SCRIPT_DIR%dist\BlindRSS-update.json"
echo [BlindRSS Build] Writing update manifest...
if defined SIGNING_THUMBPRINT (
    "%TOOL_PY%" tools\release.py write-manifest --version-tag "%VERSION_TAG%" --asset-name "%ZIP_NAME%" --sha256 "%ZIP_SHA%" --installer-asset-name "%INSTALLER_NAME%" --installer-sha256 "%INSTALLER_SHA%" --output "%MANIFEST_PATH%" --notes-summary-file "%SUMMARY_FILE%" --signing-thumbprint "!SIGNING_THUMBPRINT!"
) else (
    "%TOOL_PY%" tools\release.py write-manifest --version-tag "%VERSION_TAG%" --asset-name "%ZIP_NAME%" --sha256 "%ZIP_SHA%" --installer-asset-name "%INSTALLER_NAME%" --installer-sha256 "%INSTALLER_SHA%" --output "%MANIFEST_PATH%" --notes-summary-file "%SUMMARY_FILE%"
)
if errorlevel 1 exit /b 1
exit /b 0

:git_release
echo [BlindRSS Release] Committing version bump...
git add core\version.py CHANGELOG.md core\user_agents.py
git commit -m "Release %VERSION_TAG%"
if errorlevel 1 exit /b 1

echo [BlindRSS Release] Tagging %VERSION_TAG%...
git tag %VERSION_TAG%
if errorlevel 1 exit /b 1

echo [BlindRSS Release] Pushing branch and tag...
git push "%RELEASE_REMOTE%" HEAD
if errorlevel 1 exit /b 1
git push "%RELEASE_REMOTE%" %VERSION_TAG%
if errorlevel 1 exit /b 1

echo [BlindRSS Release] Creating GitHub release in %GITHUB_REPO_SLUG%...
gh --version >nul 2>&1
if errorlevel 1 (
    echo [X] gh CLI not found in PATH.
    exit /b 1
)
gh release create "%VERSION_TAG%" "%ZIP_PATH%" "%INSTALLER_PATH%" "%MANIFEST_PATH%" --repo "%GITHUB_REPO_SLUG%" --title "%VERSION_TAG%" --notes-file "%RELEASE_NOTES%" --latest
if errorlevel 1 exit /b 1

rem The Windows updater queries /releases/latest, which silently skips drafts.
rem gh release create has been observed leaving releases as drafts under some
rem configurations, so explicitly publish and mark this release as Latest.
echo [BlindRSS Release] Ensuring %VERSION_TAG% is published and marked as Latest...
gh release edit "%VERSION_TAG%" --repo "%GITHUB_REPO_SLUG%" --draft=false --latest
if errorlevel 1 (
    echo [X] Failed to publish %VERSION_TAG% as Latest. The Windows updater will not see it until this is resolved.
    exit /b 1
)
call :verify_no_draft_releases
if errorlevel 1 exit /b 1
call :verify_latest_release
if errorlevel 1 exit /b 1
exit /b 0

:verify_no_draft_releases
echo [BlindRSS Release] Checking for draft releases in %GITHUB_REPO_SLUG%...
powershell -NoProfile -Command "$ErrorActionPreference='Stop'; $repo='%GITHUB_REPO_SLUG%'; $releaseJson = gh release list --repo $repo --limit 100 --json tagName,isDraft; if ($LASTEXITCODE -ne 0) { exit $LASTEXITCODE }; $drafts = @($releaseJson | ConvertFrom-Json | Where-Object { $_.isDraft -eq $true }); if ($drafts.Count -gt 0) { Write-Host 'Draft releases found:'; foreach ($draft in $drafts) { Write-Host ('  ' + $draft.tagName) }; exit 1 }"
if errorlevel 1 (
    echo [X] Draft releases exist in %GITHUB_REPO_SLUG%. Publish or delete drafts manually, then rerun the release.
    exit /b 1
)
exit /b 0

:verify_latest_release
echo [BlindRSS Release] Verifying GitHub /releases/latest points to %VERSION_TAG%...
set "API_LATEST_TAG="
for /f "delims=" %%T in ('gh api "repos/%GITHUB_REPO_SLUG%/releases/latest" --jq ".tag_name" 2^>nul') do (
    set "API_LATEST_TAG=%%T"
)
if not defined API_LATEST_TAG (
    echo [X] Failed to read GitHub /releases/latest for %GITHUB_REPO_SLUG%.
    exit /b 1
)
if /I not "!API_LATEST_TAG!"=="%VERSION_TAG%" (
    echo [X] GitHub /releases/latest is !API_LATEST_TAG!, expected %VERSION_TAG%.
    echo [X] The Windows updater will keep reporting the old release until this is corrected.
    exit /b 1
)
echo [BlindRSS Release] GitHub latest release is %VERSION_TAG%.
exit /b 0

:dispatch_cross_platform_release
echo [BlindRSS Release] Dispatching GitHub Actions macOS/Linux artifact build in %GITHUB_REPO_SLUG%...
gh workflow run "cross-platform-release.yml" --repo "%GITHUB_REPO_SLUG%" --ref "%VERSION_TAG%" -f release_tag="%VERSION_TAG%"
if errorlevel 1 (
    echo [X] Failed to dispatch cross-platform GitHub Actions build.
    exit /b 1
)
echo [BlindRSS Release] macOS/Linux build dispatched for %VERSION_TAG%.
exit /b 0

:done
popd
endlocal
